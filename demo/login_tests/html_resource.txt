*** Settings ***

Documentation  A resource file containing the demo app specific keywords and variables that create our own domain specific language. Also SeleniumLibrary itself is imported here so that tests only need to import this resource file. This resource implements keywords for testing HTML version of the test application, see flex_resource.txt for Flex implementation.

Library        SeleniumLibrary


*** Variables ***

${BROWSER}       firefox
${DELAY}         0
${HOST}          localhost:7272
${LOGIN URL}     http://${HOST}/html/
${WELCOME URL}   http://${HOST}/html/welcome.html
${ERROR URL}     http://${HOST}/html/error.html
${VALID USER}    demo
${VALID PASSWD}  mode


*** Keywords ***

Open Login Page
    Open Browser  ${LOGIN URL}  ${BROWSER}
    Maximize Browser Window
    Set Selenium Speed  ${DELAY}
    Title Should Be  Login Page

Go To Login Page
    Go To  ${LOGIN URL}
    Title Should Be  Login Page

Input Username  [Arguments]  ${username}
    Input Text  username_field  ${username}

Input Password  [Arguments]  ${password}
    Input Text  password_field  ${password}

Submit Credentials
    Click Button  login_button

Welcome Page Should Be Open
    Location Should Be  ${WELCOME URL}
    Title Should Be  Welcome Page

Login Should Fail  [Arguments]  ${username}  ${password}
    Input Username  ${username}
    Input Password  ${password}
    Submit Credentials
    Location Should Be  ${ERROR URL}
    Title Should Be  Error Page
