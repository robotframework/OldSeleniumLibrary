*** Settings ***
Resource  ../resource.txt
Test Setup  Go To Page "index.html"

*** Test Cases ***
Call Selenium Python API
  ${title}=  Call Selenium API  get_title
  Should Be Equal  ${title}  (root)/index.html

Call Selenium Python API with arguments
  Call Selenium API  open  ${ROOT}/links.html
  Verify Location Is "links.html"
  ${ordered}=  Call Selenium API  is_ordered  link=Relative  link=Relative to sub-directory
  Should Be Equal  ${ordered}  ${True}

Call Selenium RC API
  ${title}=  Call Selenium API  getTitle
  Should Be Equal  ${title}  OK,(root)/index.html

Call Selenium RC API with arguments
  ${ret}=  Call Selenium API  isTextPresent  needle
  Should Be Equal  ${ret}  OK,true

Call Selenium API with non-ASCII characters
  [Setup]  Go To Page "non_ascii.html"
  :FOR  ${text}  IN  Ë  Ü  Ä  Ö
  \  ${ret}=  Call Selenium API  is_text_present  ${text}
  \  Should Be Equal  ${ret}  ${True}
  \  ${ret}=  Call Selenium API  isElementPresent  id-${text}
  \  Should Be Equal  ${ret}  OK,true

Call non-existing Selenium function
  [Documentation]  FAIL  ERROR: Unknown command: 'non_existing'
  Call Selenium API  non_existing

