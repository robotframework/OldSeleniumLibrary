*Setting*
Resource  ../resource.txt
Test Setup  Go To  ${ROOT}
Default Tags  assertions


*Test Cases*
Location Should Be
  [Documentation]  FAIL Location should have been 'non existing' but was '${ROOT}'
  ...  LOG 1 Current location is '${ROOT}'.
  Location Should Be  ${ROOT}
  Location Should Be  non existing

Location Should Contain
  [Documentation]  FAIL Location should have contained 'not a location' but it was '${ROOT}'.
  ...  LOG 1 Current location contains 'html'.
  Location Should Contain  html
  Location Should Contain  not a location

Title Should Be
  [Documentation]  FAIL Title should have been 'not a title' but was '(root)/index.html'
  ...  LOG 1 Page title is '(root)/index.html'.
  Title Should Be  (root)/index.html
  Title Should Be  not a title

Page Should Contain
  [Documentation]  FAIL Page should have contained text 'non existing text' but did not
  ...  LOG 1 Current page contains text 'needle'.
  ...  LOG 3:2 REGEXP: <head>.*</body>
  Page Should Contain  needle
  Page Should Contain  This is the haystack
  Page Should Contain  non existing text

Page Should Contain With Custom Log Level
  [Documentation]  FAIL Page should have contained text 'non existing text' but did not
  ...  LOG 1:2 DEBUG REGEXP: <head>.*</body>
  Page Should Contain  non existing text  DEBUG

Page Should Contain With Disabling Log Message
  [Documentation]  FAIL Page should have contained text 'non existing text' but did not
  ...  LOG 2:2 NONE
  Set Log Level  INFO
  Page Should Contain  non existing text  level=NONE
  [Teardown]  Set Log Level  DEBUG

Page Should Contain With Frames
  [Setup]  Go To  ${ROOT}frames/frameset.html
  Page Should Contain  You're looking at right.

Page Should Not Contain
  [Documentation]  FAIL Page should not have contained text 'needle'
  ...  LOG 1:2 Current page does not contain text 'non existing text'.
  ...  LOG 2:1 REGEXP: <head>.*</body>
  Page Should Not Contain  non existing text
  Page Should Not Contain  needle

Page Should Not Contain With Custom Log Level
  [Documentation]  FAIL Page should not have contained text 'needle'
  ...  LOG 1 DEBUG REGEXP: <head>.*</body>
  Page Should Not Contain  needle  DEBUG

Page Should Not Contain With Disabling Log Message
  [Documentation]  FAIL Page should not have contained text 'needle'
  ...  LOG 2:2 NONE
  Set Log Level  INFO
  Page Should Not Contain  needle  level=NONE
  [Teardown]  Set Log Level  DEBUG

Page Should Contain Element With Custom Message
  [Documentation]  FAIL Custom error message
  Page Should Contain Element  invalid  Custom error message

Element Should Contain
  [Documentation]  FAIL Element 'some_id' should have contained text 'non existing text' but its text was
  ...  'This text is inside an identified element'.
  Element Should Contain  some_id  This text is inside an identified element
  Element Should Contain  some_id  non existing text

Element Text Should Be
  [Documentation]  FAIL The text of element 'some_id' should have been 'inside' but in fact it was
  ...  'This text is inside an identified element'.
  Element Text Should Be  some_id  This text is inside an identified element
  Element Text Should Be  some_id  inside

Element Should Be Visible
  [Documentation]  FAIL The element 'i_am_hidden' should be visible, but it is not.
  [Setup]  Go To  ${ROOT}visibility.html
  Element Should Be Visible  i_am_visible
  Element Should Be Visible  i_am_hidden
  
Element Should Not Be Visible
  [Documentation]  FAIL The element 'i_am_visible' should not be visible, but it is.
  [Setup]  Go To  ${ROOT}visibility.html
  Element Should Not Be Visible  i_am_hidden
  Element Should Not Be Visible  i_am_visible

Page Should Contain Checkbox
  [Documentation]  FAIL Page should have contained checkbox 'non-existing' but did not LOG
  ...  1:2 Current page contains checkbox 'can_send_email'.
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Contain Checkbox  can_send_email
  Page Should Contain Checkbox  xpath=//input[@type='checkbox' and @name='can_send_sms']
  Page Should Contain Checkbox  non-existing

Page Should Not Contain Checkbox
  [Documentation]  FAIL Page should not have contained checkbox 'can_send_email'
  ...  LOG 1:2 Current page does not contain checkbox 'non-existing'.
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Not Contain Checkbox  non-existing
  Page Should Not Contain Checkbox  can_send_email

Page Should Contain Radio Button
  [Documentation]  FAIL Page should have contained radio button 'non-existing' but did not
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Contain Radio Button  sex
  Page Should Contain Radio Button  xpath=//input[@type="radio" and @value="male"]
  Page Should Contain Radio Button  non-existing

Page Should Not Contain Radio Button
  [Documentation]  FAIL Page should not have contained radio button 'sex'
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Not Contain Radio Button  non-existing
  Page Should Not Contain Radio Button  sex

Page Should Contain Element
  [Documentation]  FAIL Page should have contained element 'non-existent' but did not
  Page Should Contain Element  some_id
  Page Should Contain Element  non-existent

Page Should Not Contain Element
  [Documentation]  FAIL Page should not have contained element 'some_id'
  Page Should Not Contain Element  non-existent
  Page Should Not Contain Element  some_id

Page Should Contain Image
  [Documentation]  FAIL Page should have contained image 'non-existent' but did not
  [Setup]  Go To  ${ROOT}links.html
  Page Should contain Image  image.jpg
  Page Should contain Image  non-existent

Page Should Not Contain Image
  [Documentation]  FAIL Page should not have contained image 'image.jpg'
  [Setup]  Go To  ${ROOT}links.html
  Page Should not contain Image  non-existent
  Page Should not contain Image  image.jpg

Page Should Contain Link
  [Documentation]  FAIL Page should have contained link 'non-existent' but did not
  [Setup]  Go To  ${ROOT}links.html
  Page Should contain link  Relative
  Page Should contain link  sub/index.html
  Page Should contain link  non-existent

Page Should Not Contain Link
  [Documentation]  FAIL Page should not have contained link 'Relative'
  [Setup]  Go To  ${ROOT}links.html
  Page Should not contain link  non-existent
  Page Should not contain link  Relative

Page Should Contain List
  [Documentation]  FAIL Page should have contained list 'non-existing' but did not
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page should Contain List  possible_channels
  Page Should Contain List  non-existing

Page Should Not Contain List
  [Documentation]  FAIL Page should not have contained list 'possible_channels'
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Not Contain List  non-existing
  Page Should Not Contain List  possible_channels

Page Should Contain TextField
  [Documentation]  FAIL Page should have contained text field 'non-existing' but did not
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Contain Text Field  name
  Page Should Contain Text Field  xpath=//input[@type='text' and @name='email']
  Page Should Contain Text Field  non-existing

Page Should Not Contain Text Field
  [Documentation]  FAIL Page should not have contained text field 'name'
  [Setup]  Go To  ${ROOT}forms/prefilled_email_form.html
  Page Should Not Contain Text Field  non-existing
  Page Should Not Contain Text Field  name

TextField Should Contain
  [Documentation]  FAIL Text field 'name' should have contained text 'non-existing' but it contained 'my name'
  ...  LOG 1:2 Text field 'name' contains text ''.
  [Setup]  Go To  ${ROOT}forms/email_form.html
  TextField Should contain  name  \
  Input Text  name  my name
  TextField Should contain  name  my name
  TextField Should contain  name  non-existing

TextField Value Should Be
  [Documentation]  FAIL Value of text field 'name' should have been 'non-existing' but was 'my name'
  ...  LOG 1:2 Content of text field 'name' is ''.
  [Setup]  Go To  ${ROOT}forms/email_form.html
  textfield Value Should Be  name  \
  Input Text  name  my name
  textfield Value Should Be  name  my name
  textfield Value Should Be  name  non-existing

Page Should Contain Button
  [Documentation]  FAIL Page should have contained button 'non-existing' but did not
  [Setup]  Go To  ${ROOT}forms/buttons.html
  Page Should Contain Button  button
  Page Should Contain Button  xpath=//button[@type="submit"]
  Page Should Contain Button  Ulos
  Page Should Contain Button  xpath=//input[@type="submit"]
  Page Should Contain Button  Act!
  Page Should Contain Button  xpath=//input[@type="button"]
  Page Should Contain Button  non-existing

Page Should Not Contain Button In Button Tag
  [Documentation]  FAIL Page should not have contained button 'button'
  [Setup]  Go To  ${ROOT}forms/buttons.html
  Page Should Not Contain Button  invalid
  Page Should Not Contain Button  button

Page Should Not Contain Button In Input Tag
  [Documentation]  FAIL Page should not have contained input 'Act!'
  [Setup]  Go To  ${ROOT}forms/buttons.html
  Page Should Not Contain Button  invalid
  Page Should Not Contain Button  Act!

Get All Links
  [Setup]  Go To  ${ROOT}links.html
  ${links}=  Get All Links
  Length Should Be  ${links}  18
  List Should Contain Value  ${links}  bold_id

Xpath Should Match X Times 
  [Documentation]  FAIL Xpath //input[@type="text"] should have matched 2 times but matched 1 times
  [Setup]  Go To  ${ROOT}forms/login.html
  Xpath Should Match X Times  //input[@type="text"]  1
  Xpath Should Match X Times  //input[@type="text"]  ${1}
  Xpath Should Match X Times  //input[@type="text"]  2
