*** Settings ***
Suite Setup     Go To  ${ROOT}/flex/flex.html
Test Setup      Select Simple Flex Application
Force Tags      ci-incompatible
Resource        ../resource.txt

*** Test Cases ***
Using Flex commands without selecting application
    [Documentation]  FAIL No Flex application selected.
    [Setup]
    Click Flex Element  button

Input text
    Input Text Into Flex  inputField  RF <3 FP
    Text In Flex Should Be  inputField  RF &lt;3 FP

Click Element
    Input Text Into Flex  inputField  uppercase?
    Click Flex Element  upperButton
    Click Flex Element  OK
    Text In Flex Should Be  inputField  UPPERCASE?

Text in flex should be
    Input Text Into Flex    inputField  testing
    Text In Flex Should Be  inputField  testing
    Text In Flex Should Be  helloLabel  Hello Flex!
    Text In Flex Should Be  upperButton  Upper

Text in flex should be fails
    [Documentation]  FAIL ERROR: Expected "Hi Silverlight!", got "Hello Flex!"
    Text In Flex Should Be  helloLabel  Hi Silverlight!

Text in flex should be when element doesn't exist
    [Documentation]  FAIL ERROR: The chain 'name:nonExisting' was not found.
    Text In Flex Should Be  nonExisting  Whatever

*** Keywords ***
Select Simple Flex Application
    Select Flex Application  testApp
