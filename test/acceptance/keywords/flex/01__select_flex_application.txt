*** Settings ***
Suite Setup     Go To Page With Multiple Flex Apps
Resource        flex_resource.txt


*** Test Cases ***
Using Flex commands without selecting application
    No Flex Application Should Be Selected

Select application
    [Template]  Test Selecting Application
    ${COMPONENT APP}  ${None}          Component Application Should Be Selected
    ${DYNAMIC APP}    ${COMPONENTAPP}  Dynamic Application Should Be Selected
    ${None}           ${DYNAMIC APP}   No Flex Application Should Be Selected

*** Keywords ***

No Flex Application Should Be Selected
    Run Keyword And Expect Error  ${NO FLEX APP ERROR}  Click Flex Element  someComponent

Component Application Should Be Selected
    Flex Element Should Exist  languageComboBox

Dynamic Application Should Be Selected
    Flex Element Should Exist  clickCountLabel

Test Selecting Application
    [Arguments]  ${newapp}  ${oldapp}  ${application should be selected}
    ${old} =  Select Flex Application  ${newapp}
    Should Be Equal  ${old}  ${oldapp}
    Run Keyword  ${application should be selected}

