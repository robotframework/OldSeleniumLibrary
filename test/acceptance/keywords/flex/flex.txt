*** Settings ***
Suite Setup     Go To  ${ROOT}/flex/flex.html
Test Setup      Select Login Application
Resource        ../../resource.txt

*** Variables ***
${some username}  James Bond
${HELP TEXT}  Input your username and password to log in

*** Test Cases ***
Input text
    Input Text Into Flex Element  username  ${some username}
    Flex Element Text Should Be  username  ${some username}

Click Element
    Flex Element Text Should Be  helpLabel  ${EMPTY}
    Click Flex Element  helpButton
    Flex Element Text Should Be  helpLabel  ${HELP TEXT}

Text in flex should be with buttons
    Flex Element Text Should Be  helpButton  Help

Text in flex should be fails
    Run Keyword And Expect Error  ERROR: Expected "not there", got "Help"  Flex Element Text Should Be  helpButton  not there

Text in flex should be when element doesn't exist
    Run Keyword And Expect Error  ERROR: The chain 'id:nonExisting' was not found.  Flex Element Text Should Be  nonExisting  Whatever

*** Keywords ***
Select Login Application
    Select Flex Application  loginApp

