*** Settings ***
Resource  ../resource.txt
Suite Setup  Go To Page "tables/tables.html"

*** Test Cases ***

Should Find Text In Table Content
    Table Should Contain  simpleTable            simpleTable_A1
    Table Should Contain  simpleTable            simpleTable_C3
    Table Should Contain  simpleWithNested       simpleWithNested_A1
    Table Should Contain  simpleWithNested       nestedTable_A1
    Table Should Contain  nestedTable            nestedTable_A1
    Table Should Contain  nestedTable            nestedTable_C3
    Table Should Contain  tableWithSingleHeader  tableWithSingleHeader_B1
    Table Should Contain  tableWithSingleHeader  tableWithSingleHeader_B2
    Table Should Contain  withHeadAndFoot        withHeadAndFoot_AH1
    Table Should Contain  withHeadAndFoot        withHeadAndFoot_B2
    Table Should Contain  withHeadAndFoot        withHeadAndFoot_CF1
    Table Should Contain  mergedRows             mergedRows_A1
    Table Should Contain  mergedCols             mergedCols_B2
    Table Should Contain  formattedTable         formattedTable_A1
    Table Should Contain  formattedTable         formattedTable_B1
    Table Should Contain  formattedTable         formattedTable_C1
    Table Should Contain  formattedTable         formattedTable_D1
    Table Should Contain  formattedTable         formattedTable_A2
    Table Should Contain  formattedTable         formattedTable_B2
    Table Should Contain  formattedTable         formattedTable_ÄÖÜäöüß
    Table Should Contain  formattedTable         äöü€&äöü€&

Should Give Error Message When Content Not Found In Table
    [Documentation]  FAIL Table identified by 'simpleTable' should have contained text 'XXX'.
    Table Should Contain  simpleTable  XXX

Should Find Text In Header
    Table Header Should Contain  tableWithSingleHeader  tableWithSingleHeader_A1
    Table Header Should Contain  tableWithSingleHeader  tableWithSingleHeader_B1
    Table Header Should Contain  tableWithTwoHeaders    tableWithTwoHeaders_A1
    Table Header Should Contain  tableWithTwoHeaders    tableWithTwoHeaders_C2
    Table Header Should Contain  withHeadAndFoot        withHeadAndFoot_AH1
    Table Header Should Contain  withHeadAndFoot        withHeadAndFoot_CH1
    Table Header Should Contain  withHeadAndFoot        withHeadAndFoot_AH2
    Table Header Should Contain  withHeadAndFoot        withHeadAndFoot_BH2

Should Give Error Message When Content Not Found In Table Header
    [Documentation]  FAIL Header in table identified by 'withHeadAndFoot' should have contained text 'withHeadAndFoot_B2'.
    Table Header Should Contain  withHeadAndFoot  withHeadAndFoot_B2

Should Find Text In Footer
    Table Footer Should Contain  withHeadAndFoot  withHeadAndFoot_AF1
    Table Footer Should Contain  withHeadAndFoot  withHeadAndFoot_CF1
    Table Footer Should Contain  withHeadAndFoot  withHeadAndFoot_AF2
    Table Footer Should Contain  withHeadAndFoot  withHeadAndFoot_CF2

Should Give Error Message When Content Not Found In Table Footer
    [Documentation]  FAIL Footer in table identified by 'withHeadAndFoot' should have contained text 'withHeadAndFoot_B2'.
    Table Footer Should Contain  withHeadAndFoot  withHeadAndFoot_B2

Should Find Text In Specific Row
    Table Row Should Contain  simpleTable            1  simpleTable_A1
    Table Row Should Contain  simpleTable            3  simpleTable_C3
    Table Row Should Contain  simpleWithNested       1  simpleWithNested_A1
    Table Row Should Contain  simpleWithNested       1  nestedTable_A1
    Table Row Should Contain  nestedTable            1  nestedTable_A1
    Table Row Should Contain  nestedTable            3  nestedTable_C3
    Table Row Should Contain  tableWithSingleHeader  1  tableWithSingleHeader_B1
    Table Row Should Contain  tableWithSingleHeader  2  tableWithSingleHeader_B2
    Table Row Should Contain  withHeadAndFoot        2  withHeadAndFoot_B2
    Table Row Should Contain  mergedRows             1  mergedRows_A1
    Table Row Should Contain  mergedCols             2  mergedCols_B2
    Table Row Should Contain  formattedTable         1  formattedTable_A1
    Table Row Should Contain  formattedTable         1  formattedTable_B1
    Table Row Should Contain  formattedTable         1  formattedTable_C1
    Table Row Should Contain  formattedTable         1  formattedTable_D1
    Table Row Should Contain  formattedTable         2  formattedTable_A2
    Table Row Should Contain  formattedTable         2  formattedTable_B2
    Table Row Should Contain  formattedTable         2  formattedTable_ÄÖÜäöüß
    Table Row Should Contain  formattedTable         2  äöü€&äöü€&

Should Give Error Message When Content Not Found In Table Row
    [Documentation]  FAIL Row #2 in table identified by 'simpleTable' should have contained text 'simpleTable_B3'.
    Table Row Should Contain  simpleTable  2  simpleTable_B3

Should Give Error Message When Row Number Out Of Bounds
    [Documentation]  FAIL Row #20 in table identified by 'simpleTable' should have contained text 'simpleTable_B3'.
    Table Row Should Contain  simpleTable  20  simpleTable_B3

Should Find Text In Specific Column
    Table Column Should Contain  simpleTable            2  simpleTable_B2
    Table Column Should Contain  simpleWithNested       2  nestedTable_A1
    Table Column Should Contain  nestedTable            3  nestedTable_C1
    Table Column Should Contain  tableWithSingleHeader  1  tableWithSingleHeader_A1
    Table Column Should Contain  tableWithSingleHeader  1  tableWithSingleHeader_A3
    Table Column Should Contain  tableWithTwoHeaders    2  tableWithTwoHeaders_B2

Should Give Error Message When Content Not Found In Table Column
    [Documentation]  FAIL Column #2 in table identified by 'simpleTable' should have contained text 'simpleTable_C3'.
    Table Column Should Contain  simpleTable  2  simpleTable_C3

Should Give Error Message When Column Number Out Of Bounds
    [Documentation]  FAIL Column #20 in table identified by 'simpleTable' should have contained text 'simpleTable_B3'.
    Table Column Should Contain  simpleTable  20  simpleTable_B3

Should Retrieve Text From Cell
    Table Cell Should Be Equal  simpleTable            1  1  simpleTable_A1
    Table Cell Should Be Equal  tableWithSingleHeader  1  1  tableWithSingleHeader_A1
    Table Cell Should Be Equal  tableWithSingleHeader  3  3  tableWithSingleHeader_C3
    Table Cell Should Be Equal  withHeadAndFoot        1  2  withHeadAndFoot_BH1
    Table Cell Should Be Equal  withHeadAndFoot        4  3  withHeadAndFoot_C2
    Table Cell Should Be Equal  withHeadAndFoot        6  1  withHeadAndFoot_AF1
    Table Cell Should Be Equal  mergedRows             2  1  mergedRows_B2
    Table Cell Should Be Equal  mergedCols             1  2  mergedCols_C1
    Table Cell Should Be Equal  formattedTable         1  1  formattedTable_A1
    Table Cell Should Be Equal  formattedTable         1  4  formattedTable_D1
    Table Cell Should Be Equal  formattedTable         2  2  formattedTable_B2
    Table Cell Should Be Equal  formattedTable         2  3  formattedTable_ÄÖÜäöüß
    Table Cell Should Be Equal  formattedTable         2  4  äöü€&äöü€&

Should Give Error Message When Content Not Found In Table Cell
    [Documentation]  FAIL Cell in table 'simpleTable' in row #1 and column #2 should have contained text 'simpleTable_B3'.
    Table Cell Should Contain  simpleTable  1  2  simpleTable_B3

Should Give Error Message When Index Out Of Bounds
    [Documentation]  FAIL Cell in table 'simpleTable' in row #10 and column #20 should have contained text 'simpleTable_B3'.
    Table Cell Should Contain  simpleTable  10  20  simpleTable_B3

Should Identify Table By CSS
    Table Should Contain         css=table#simpleTable                        simpleTable
    Table Header Should Contain  css=table#tableWithTwoHeaders                tableWithTwoHeaders_C2
    Table Footer Should Contain  css=table#withHeadAndFoot                    withHeadAndFoot_AF1
    Table Row Should Contain     css=table#mergedRows                      1  mergedRows_D1
    Table Column Should Contain  css=table#mergedCols                      1  mergedCols_D1
    Table Cell Should Contain    css=table#formattedTable               1  1  formattedTable_A1
    Table Cell Should Be Equal   css=table#formattedTable               2  4  äöü€&äöü€&
    Table Cell Should Be Equal   css=h2.someClass ~ table:last-child()  2  4  äöü€&äöü€&

*** Keywords ***

Table Cell Should Be Equal  [Arguments]  ${tableId}  ${row}  ${col}  ${content}
    ${cell}=  Get Table Cell  ${tableId}  ${row}  ${col}
    Should Be Equal  ${cell}  ${content}
